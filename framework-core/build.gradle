apply plugin: 'com.android.library'
apply plugin: 'maven'

android {
    compileSdkVersion 23
    buildToolsVersion "23.0.3"
    lintOptions {
        abortOnError false
    }
    defaultConfig {
        minSdkVersion 15
        targetSdkVersion 23
        versionCode 1
        versionName "1.0"
    }
    buildTypes {
        release {
            zipAlignEnabled true
            minifyEnabled false
            shrinkResources true  //去除无用资源
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
}

def outJarVersion = "framework-" + android.defaultConfig.versionName + ".jar"

/**
 * 定义一个函数
 * @param target target是生成jar包的文件名
 * @param classDir classDir是class文件所在的文件夹
 * @return
 */
def makeJar(String target, String classDir) {
    exec {
        executable "jar"   //调用jar
        args "cvf", target
        args "-C", classDir
        args "", "."
    }
}

/**
 * 新建一个task,名为buildJar,依赖build(build是一个自带的task)
 */
task buildJar(dependsOn: ['build']) << {
    makeJar(outJarVersion, "build/intermediates/classes/release")
}

task buildProguardJar(type: Copy) {
    from("build/intermediates/bundles/release")
    into("/proguardJar")
    include("classes.jar")
    rename("classes.jar", outJarVersion)
}

buildProguardJar.dependsOn(build)

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    testCompile 'junit:junit:4.12'
    compile 'com.android.support:support-v4:23.+'
    compile 'com.android.support:appcompat-v7:23.+'
    compile 'com.android.support:recyclerview-v7:23.+'
    compile 'com.github.bumptech.glide:glide:3.7.0'
    compile 'com.squareup.okhttp3:okhttp:3.2.0'
    compile 'com.android.volley:volley:1.0.0'
}

// ext is a gradle closure allowing the declaration of global properties
ext {
    PUBLISH_GROUP_ID = 'com.lgmshare.wukong'
    PUBLISH_ARTIFACT_ID = 'core'
    PUBLISH_VERSION = android.defaultConfig.versionName
}

uploadArchives {
    repositories.mavenDeployer {
        def deployPath = file(getProperty('aar.deployPath'))
        repository(url: "file://${deployPath.absolutePath}")
        pom.project {
            groupId project.PUBLISH_GROUP_ID
            artifactId project.PUBLISH_ARTIFACT_ID
            version project.PUBLISH_VERSION
        }
    }
}
